apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "net.vonforst.evmap"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 20
        versionName "0.2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    flavorDimensions "dependencies"
    productFlavors {
        foss {
            dimension "dependencies"
            versionNameSuffix "-foss"
        }
        google {
            dimension "dependencies"
            versionNameSuffix "-google"
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"
    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("../_ci/keystore.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding = true
    }

    // add API keys from environment variable if not set in apikeys.xml
    applicationVariants.all { variant ->
        ext.env = System.getenv()
        def goingelectricKey = env.GOINGELECTRIC_API_KEY
        if (goingelectricKey != null) {
            variant.resValue "string", "goingelectric_key", goingelectricKey
        }
        def googleMapsKey = env.GOOGLE_MAPS_API_KEY
        if (googleMapsKey != null) {
            variant.resValue "string", "google_maps_key", googleMapsKey
        }
        def mapboxKey = env.MAPBOX_API_KEY
        if (mapboxKey != null) {
            variant.resValue "string", "mapbox_key", mapboxKey
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.2.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'com.github.johan12345:CustomBottomSheetBehavior:f69f532660'
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.7.2'
    implementation 'com.squareup.moshi:moshi-kotlin:1.9.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.MikeOrtiz:TouchImageView:2.3.3'
    implementation "com.mikepenz:aboutlibraries-core:$about_libs_version"
    implementation "com.mikepenz:aboutlibraries:$about_libs_version"
    implementation 'com.airbnb.android:lottie:3.4.0'
    implementation 'io.michaelrocks:bimap:1.0.2'
    implementation 'com.mapzen.android:lost:3.0.2'

    // AnyMaps
    def anyMapsVersion = 'cc613d7a29'
    implementation "com.github.johan12345.AnyMaps:anymaps-base:$anyMapsVersion"
    googleImplementation "com.github.johan12345.AnyMaps:anymaps-google:$anyMapsVersion"
    implementation "com.github.johan12345.AnyMaps:anymaps-mapbox:$anyMapsVersion"

    // Google Maps v3 Beta
    googleImplementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    googleImplementation name:'places-maps-sdk-3.1.0-beta', ext:'aar'
    googleImplementation 'com.android.volley:volley:1.1.1'
    googleImplementation 'com.google.android.gms:play-services-base:17.3.0'
    googleImplementation 'com.google.android.gms:play-services-basement:17.3.0'
    googleImplementation 'com.google.android.gms:play-services-gcm:17.0.0'
    googleImplementation 'com.google.android.gms:play-services-location:17.0.0'
    googleImplementation 'com.google.android.gms:play-services-tasks:17.1.0'
    googleImplementation 'com.google.auto.value:auto-value-annotations:1.6.3'
    googleImplementation 'com.google.code.gson:gson:2.8.6'
    googleImplementation 'com.google.android.datatransport:transport-runtime:2.2.3'
    googleImplementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // navigation library
    def nav_version = "2.3.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // viewmodel library
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // room library
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // billing library
    def billing_version = "3.0.0"
    googleImplementation "com.android.billingclient:billing:$billing_version"
    googleImplementation "com.android.billingclient:billing-ktx:$billing_version"

    // debug tools
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    testImplementation 'junit:junit:4.13'
    testImplementation "com.squareup.okhttp3:mockwebserver:3.14.7"
    //noinspection GradleDependency
    testImplementation 'org.json:json:20080701'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'
}